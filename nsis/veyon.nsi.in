!define DLLDIR "@MINGW_PREFIX@/bin"
!define DLLDIR_LIB "@MINGW_PREFIX@/lib"
!define DLLDIR_GCC "/usr/lib/gcc/@CMAKE_SYSTEM_PROCESSOR@-w64-mingw32/5.3-win32"
!if "@CMAKE_SYSTEM_PROCESSOR@" == "x86_64"
   !define DLL_GCC "libgcc_s_seh-1.dll"
!else
   !define DLL_GCC "libgcc_s_sjlj-1.dll"
!endif
!define QT5_PLUGINS_PLATFORMS "/usr/@CMAKE_SYSTEM_PROCESSOR@-w64-mingw32/lib/qt5/plugins/platforms"
!define APP_NAME "Veyon"
!define COMP_NAME "Veyon Solutions Inc."
!define WEB_SITE "http://veyon.io"
!define VERSION "@VERSION_MAJOR@.@VERSION_MINOR@.@VERSION_PATCH@.@VERSION_BUILD@"
!define COPYRIGHT "Â© Tobias Doerffel 2004-2017"
!define DESCRIPTION "Virtual Eye On Networks"
!define LICENSE_TXT "@CMAKE_SOURCE_DIR@/COPYING"
!define INSTALLER_NAME "veyon-${VERSION}-@MINGW_PLATFORM@-setup.exe"
!define MAIN_APP_EXE "veyon-master.exe"
!define INSTALL_TYPE "SetShellVarContext all"
!define REG_ROOT "HKLM"
!define REG_APP_PATH "Software\Microsoft\Windows\CurrentVersion\App Paths\${MAIN_APP_EXE}"
!define UNINSTALL_PATH "Software\Microsoft\Windows\CurrentVersion\Uninstall\${APP_NAME}"

######################################################################

VIProductVersion  "${VERSION}"
VIAddVersionKey "ProductName"  "${APP_NAME}"
VIAddVersionKey "CompanyName"  "${COMP_NAME}"
VIAddVersionKey "LegalCopyright"  "${COPYRIGHT}"
VIAddVersionKey "FileDescription"  "${DESCRIPTION}"
VIAddVersionKey "FileVersion"  "${VERSION}"

######################################################################

SetCompressor /SOLID Lzma
Name "${APP_NAME}"
Caption "${APP_NAME} ${VERSION}"
OutFile "${INSTALLER_NAME}"
BrandingText "${APP_NAME} ${VERSION}"
XPStyle on
InstallDirRegKey "${REG_ROOT}" "${REG_APP_PATH}" ""
InstallDir "$PROGRAMFILES64\Veyon"
ShowInstDetails show
RequestExecutionLevel highest

######################################################################

!include "MUI2.nsh"
!include "FileFunc.nsh"

!define MUI_ABORTWARNING
!define MUI_UNABORTWARNING

!define MUI_WELCOMEFINISHPAGE_BITMAP "@CMAKE_SOURCE_DIR@/nsis/welcome-page.bmp"
!define MUI_UNWELCOMEFINISHPAGE_BITMAP "@CMAKE_SOURCE_DIR@/nsis/welcome-page.bmp" 

!define MUI_ICON "@CMAKE_SOURCE_DIR@/nsis/installer.ico"
!define MUI_UNICON "@CMAKE_SOURCE_DIR@/nsis/uninstaller.ico"
!define MUI_HEADERIMAGE
!define MUI_HEADERIMAGE_BITMAP "@CMAKE_SOURCE_DIR@/nsis/header.bmp"
!define MUI_HEADERIMAGE_RIGHT

!define MUI_LANGDLL_REGISTRY_ROOT "${REG_ROOT}"
!define MUI_LANGDLL_REGISTRY_KEY "${UNINSTALL_PATH}"
!define MUI_LANGDLL_REGISTRY_VALUENAME "Installer Language"

!insertmacro MUI_PAGE_WELCOME

!insertmacro MUI_PAGE_LICENSE "${LICENSE_TXT}"

!insertmacro MUI_PAGE_DIRECTORY

!insertmacro MUI_PAGE_COMPONENTS

!insertmacro MUI_PAGE_INSTFILES

!define MUI_FINISHPAGE_RUN
!define MUI_FINISHPAGE_RUN_TEXT "Run Veyon Configurator now"
!define MUI_FINISHPAGE_RUN_FUNCTION "RunConfigurator"

!insertmacro MUI_PAGE_FINISH

!insertmacro MUI_UNPAGE_CONFIRM

!insertmacro MUI_UNPAGE_INSTFILES

!insertmacro MUI_UNPAGE_FINISH

!insertmacro MUI_LANGUAGE "Albanian"
!insertmacro MUI_LANGUAGE "Arabic"
!insertmacro MUI_LANGUAGE "Belarusian"
!insertmacro MUI_LANGUAGE "Bosnian"
!insertmacro MUI_LANGUAGE "Breton"
!insertmacro MUI_LANGUAGE "Bulgarian"
!insertmacro MUI_LANGUAGE "Catalan"
!insertmacro MUI_LANGUAGE "Croatian"
!insertmacro MUI_LANGUAGE "Czech"
!insertmacro MUI_LANGUAGE "Danish"
!insertmacro MUI_LANGUAGE "Dutch"
!insertmacro MUI_LANGUAGE "English"
!insertmacro MUI_LANGUAGE "Estonian"
!insertmacro MUI_LANGUAGE "Farsi"
!insertmacro MUI_LANGUAGE "Finnish"
!insertmacro MUI_LANGUAGE "French"
!insertmacro MUI_LANGUAGE "German"
!insertmacro MUI_LANGUAGE "Greek"
!insertmacro MUI_LANGUAGE "Hebrew"
!insertmacro MUI_LANGUAGE "Hungarian"
!insertmacro MUI_LANGUAGE "Icelandic"
!insertmacro MUI_LANGUAGE "Indonesian"
!insertmacro MUI_LANGUAGE "Italian"
!insertmacro MUI_LANGUAGE "Japanese"
!insertmacro MUI_LANGUAGE "Korean"
!insertmacro MUI_LANGUAGE "Kurdish"
!insertmacro MUI_LANGUAGE "Latvian"
!insertmacro MUI_LANGUAGE "Lithuanian"
!insertmacro MUI_LANGUAGE "Luxembourgish"
!insertmacro MUI_LANGUAGE "Macedonian"
!insertmacro MUI_LANGUAGE "Malay"
!insertmacro MUI_LANGUAGE "Mongolian"
!insertmacro MUI_LANGUAGE "Norwegian"
!insertmacro MUI_LANGUAGE "Polish"
!insertmacro MUI_LANGUAGE "Portuguese"
!insertmacro MUI_LANGUAGE "PortugueseBR"
!insertmacro MUI_LANGUAGE "Romanian"
!insertmacro MUI_LANGUAGE "Russian"
!insertmacro MUI_LANGUAGE "Serbian"
!insertmacro MUI_LANGUAGE "SerbianLatin"
!insertmacro MUI_LANGUAGE "SimpChinese"
!insertmacro MUI_LANGUAGE "Slovak"
!insertmacro MUI_LANGUAGE "Slovenian"
!insertmacro MUI_LANGUAGE "Spanish"
!insertmacro MUI_LANGUAGE "Swedish"
!insertmacro MUI_LANGUAGE "Thai"
!insertmacro MUI_LANGUAGE "TradChinese"
!insertmacro MUI_LANGUAGE "Turkish"
!insertmacro MUI_LANGUAGE "Ukrainian"

!insertmacro MUI_RESERVEFILE_LANGDLL

######################################################################

Section "Veyon Service"
SectionIn RO
${INSTALL_TYPE}
ExecWait '"$INSTDIR\veyon-service.exe" -quiet -unregisterservice'
SetOverwrite on
SetOutPath "$INSTDIR"
# DLLs
File "${DLLDIR}\Qt5Core.dll"
File "${DLLDIR}\Qt5Gui.dll"
File "${DLLDIR}\Qt5Widgets.dll"
File "${DLLDIR}\Qt5Network.dll"
File "${DLLDIR}\libjpeg-62.dll"
File "${DLLDIR}\libeay32.dll"
File "${DLLDIR}\libssl32.dll"
File "${DLLDIR}\libqca-qt5.dll"
File "${DLLDIR}\libpng16-16.dll"
File "${DLLDIR}\liblber.dll"
File "${DLLDIR}\libldap.dll"
File "${DLLDIR}\interception.dll"
File "${DLLDIR}\liblzo2-2.dll"
File "${DLLDIR_LIB}\zlib1.dll"
File "${DLLDIR_LIB}\libwinpthread-1.dll"
File "${DLLDIR_GCC}\libstdc++-6.dll"
File "${DLLDIR_GCC}\${DLL_GCC}"
# core components
File "veyon-service.exe"
File "vnchooks.dll"
File "veyon-core.dll"
File "veyon-configurator.exe"
File "veyon-ctl.exe"
File "veyon-worker.exe"
File "LICENSE.TXT"
File "README.TXT"
File "install-interception.exe"
SetOutPath "$INSTDIR\crypto"
File "${DLLDIR_LIB}\qca-qt5\crypto\libqca-ossl.dll"
SetOutPath "$INSTDIR\platforms"
File "${QT5_PLUGINS_PLATFORMS}/qwindows.dll"
SetOutPath "$INSTDIR\plugins"
File "plugins/*.dll"
SetOutPath "$INSTDIR\doc"
File "doc\*txt"
SetOutPath "$INSTDIR\contrib"
File "contrib\*"
SetOutPath "$INSTDIR\contrib\en-US"
File "contrib\en-US\*"
ExecWait '"$INSTDIR\install-interception.exe" /install'
ExecWait '"$INSTDIR\veyon-service.exe" -quiet -registerservice'
ExecWait '"$INSTDIR\veyon-ctl.exe" config set Network/FirewallExceptionEnabled 1'
ExecWait '"$INSTDIR\veyon-ctl.exe" config set Service/SoftwareSASEnabled 1'
# disable forced guest logon as otherwise logon authentication fails
WriteRegDWORD ${REG_ROOT} "SYSTEM\CurrentControlSet\Control\Lsa"  "ForceGuest" "0"
${GetParameters} $R0
${GetOptions} $R0 "/ApplyConfig=" $R1
${If} $R1 != ""
  ExecWait '"$INSTDIR\veyon-ctl.exe" config import $R1'
${EndIf}
ExecWait '"$INSTDIR\veyon-service.exe" -quiet -startservice'
SectionEnd

Section "Veyon Master" SEC_MASTER
${INSTALL_TYPE}
SetOutPath "$INSTDIR"
File "veyon-master.exe"

CreateDirectory "$SMPROGRAMS\Veyon"
CreateShortCut "$SMPROGRAMS\Veyon\Veyon Master.lnk" "$INSTDIR\${MAIN_APP_EXE}"
CreateShortCut "$DESKTOP\Veyon Master.lnk" "$INSTDIR\${MAIN_APP_EXE}"
SectionEnd

Function RunConfigurator
	Exec '"$INSTDIR\veyon-configurator.exe"'
FunctionEnd


######################################################################

Function .onInit
#!insertmacro MUI_LANGDLL_DISPLAY
${GetParameters} $R0
${GetOptions} $R0 "/NoMaster" $R1
${IfNot} ${Errors}
  !insertmacro UnselectSection ${SEC_MASTER}
${EndIf}
FunctionEnd

######################################################################

Section -Icons_Reg
SetOutPath "$INSTDIR"
WriteUninstaller "$INSTDIR\uninstall.exe"

CreateDirectory "$SMPROGRAMS\Veyon"
CreateShortCut "$SMPROGRAMS\Veyon\Veyon Configurator.lnk" "$INSTDIR\veyon-configurator.exe"
CreateShortCut "$SMPROGRAMS\Veyon\Uninstall Veyon.lnk" "$INSTDIR\uninstall.exe"
WriteIniStr "$INSTDIR\Veyon website.url" "InternetShortcut" "URL" "${WEB_SITE}"
CreateShortCut "$SMPROGRAMS\Veyon\Veyon Website.lnk" "$INSTDIR\Veyon website.url"

WriteRegStr ${REG_ROOT} "${REG_APP_PATH}" "" "$INSTDIR\${MAIN_APP_EXE}"
WriteRegStr ${REG_ROOT} "${UNINSTALL_PATH}"  "DisplayName" "Veyon"
WriteRegStr ${REG_ROOT} "${UNINSTALL_PATH}"  "UninstallString" "$INSTDIR\uninstall.exe"
WriteRegStr ${REG_ROOT} "${UNINSTALL_PATH}"  "DisplayIcon" "$INSTDIR\${MAIN_APP_EXE}"
WriteRegStr ${REG_ROOT} "${UNINSTALL_PATH}"  "DisplayVersion" "${VERSION}"
WriteRegStr ${REG_ROOT} "${UNINSTALL_PATH}"  "Publisher" "${COMP_NAME}"
WriteRegStr ${REG_ROOT} "${UNINSTALL_PATH}"  "URLInfoAbout" "${WEB_SITE}"
SectionEnd

######################################################################

Section Uninstall
${INSTALL_TYPE}

ExecWait '"$INSTDIR\veyon-ctl.exe" config set Service/SoftwareSASEnabled 0'
ExecWait '"$INSTDIR\veyon-ctl.exe" config set Network/FirewallExceptionEnabled 0'
ExecWait '"$INSTDIR\veyon-service.exe" -quiet -unregisterservice'
ExecWait '"$INSTDIR\install-interception.exe" /uninstall'
# make sure the service is stopped and all file handles are released
Sleep 2000

${GetParameters} $R0
${GetOptions} $R0 "/ClearConfig" $R1
${IfNot} ${Errors}
	ExecWait '"$INSTDIR\veyon-ctl.exe" config clear'
${EndIf}

Delete "$INSTDIR\veyon-service.exe"
Delete "$INSTDIR\libwinpthread-1.dll"
Delete "$INSTDIR\libgcc_s_sjlj-1.dll"
Delete "$INSTDIR\libgcc_s_seh-1.dll"
Delete "$INSTDIR\libstdc++-6.dll"
Delete "$INSTDIR\zlib1.dll"
Delete "$INSTDIR\liblzo2-2.dll"
Delete "$INSTDIR\libjpeg-62.dll"
Delete "$INSTDIR\libeay32.dll"
Delete "$INSTDIR\libpng16-16.dll"
Delete "$INSTDIR\liblber.dll"
Delete "$INSTDIR\libldap.dll"
Delete "$INSTDIR\interception.dll"
Delete "$INSTDIR\Qt5Network.dll"
Delete "$INSTDIR\Qt5Core.dll"
Delete "$INSTDIR\libssl32.dll"
Delete "$INSTDIR\libqca-qt5.dll"
Delete "$INSTDIR\LICENSE.TXT"
Delete "$INSTDIR\vnchooks.dll"
Delete "$INSTDIR\veyon-configurator.exe"
Delete "$INSTDIR\veyon-ctl.exe"
Delete "$INSTDIR\veyon-worker.exe"
Delete "$INSTDIR\veyon-master.exe"
Delete "$INSTDIR\README.TXT"
Delete "$INSTDIR\veyon-core.dll"
Delete "$INSTDIR\Qt5Gui.dll"
Delete "$INSTDIR\Qt5Widgets.dll"
Delete "$INSTDIR\install-interception.exe"
Delete "$INSTDIR\uninstall.exe"
Delete "$INSTDIR\Veyon website.url"
RmDir /r "$INSTDIR\crypto"
RmDir /r "$INSTDIR\plugins"
RmDir /r "$INSTDIR\platforms"
RmDir /r "$INSTDIR\doc"
RmDir /r "$INSTDIR\contrib"

RmDir "$INSTDIR"

Delete "$SMPROGRAMS\Veyon\Veyon Master.lnk"
Delete "$SMPROGRAMS\Veyon\Veyon Configurator.lnk"
Delete "$SMPROGRAMS\Veyon\Uninstall Veyon.lnk"
Delete "$SMPROGRAMS\Veyon\Veyon Website.lnk"
RmDir "$SMPROGRAMS\Veyon"

Delete "$DESKTOP\Veyon Master.lnk"

DeleteRegKey ${REG_ROOT} "${REG_APP_PATH}"
DeleteRegKey ${REG_ROOT} "${UNINSTALL_PATH}"
SectionEnd

######################################################################

Function un.onInit
!insertmacro MUI_UNGETLANGUAGE
FunctionEnd

######################################################################

